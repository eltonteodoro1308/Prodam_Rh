#INCLUDE "PROTHEUS.CH"
#INCLUDE "APDA040.CH"

/*/{Protheus.doc} APDA040
Cadastro de Tipos de Avalica‡„o.
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 03/07/2014
@param cAlias, characters, cAlias
@param nReg, numeric, nReg
@param nOpc, numeric, nOpc
@param lExecAuto, logical, lExecAuto
/*/
Function APDA040( cAlias , nReg , nOpc , lExecAuto )

	Local aArea 	:= GetArea()
	Local aAreaRd5	:= RD5->( GetArea() )
	Local aAreaRdh	:= RDH->( GetArea() )
	Local lExistOpc	:= ( ValType( nOpc ) == "N" )

	Begin Sequence

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³So Executa se o Modo de Acesso dos Arquivos do Modulo APD esti³
		³verem OK													   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF !( ApdRelationFile() )
			Break
		EndIF

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Redefine o Alias                                              ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		cAlias	:= "RD5"

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Define Array contendo as Rotinas a executar do programa      ³
		³ ----------- Elementos contidos por dimensao ------------     ³
		³ 1. Nome a aparecer no cabecalho                              ³
		³ 2. Nome da Rotina associada                                  ³
		³ 3. Usado pela rotina                                         ³
		³ 4. Tipo de Transa‡„o a ser efetuada                          ³
		³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
		³    2 - Simplesmente Mostra os Campos                         ³
		³    3 - Inclui registros no Bancos de Dados                   ³
		³    4 - Altera o registro corrente                            ³
		³    5 - Remove o registro corrente do Banco de Dados          ³
		³    6 - Copiar                                                ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		Private aRotina := MenuDef() // ajuste para versao 9.12 - chamada da funcao MenuDef() que contem aRotina

		Private cCadastro	:= OemToAnsi( STR0006 )	//"Cadastro de Tipos de Avalica‡„o"

		IF ( lExistOpc )

			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Garante o Posicinamento do Recno                              ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			DEFAULT nReg	:= ( cAlias )->( Recno() )
			IF !Empty( nReg )
				( cAlias )->( MsGoto( nReg ) )
			EndIF

			DEFAULT lExecAuto := .F.
			IF ( lExecAuto )

				nPos := aScan( aRotina , { |x| x[4] == nOpc } )
				IF ( nPos == 0 )
					Break
				EndIF
				bBlock := &( "{ |a,b,c,d| " + aRotina[ nPos , 2 ] + "(a,b,c,d) }" )
				Eval( bBlock , cAlias , nReg , nPos )

			Else

				Apda040Mnt( cAlias , nReg , nOpc , .T. )

			EndIF

		Else

			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Chama a Funcao de Montagem do Browse                                   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			mBrowse( 6 , 1 , 22 , 75 , cAlias )

		EndIF

	End Sequence

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Coloca o Ponteiro do Mouse em Estado de Espera			   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	CursorWait()

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Restaura os Dados de Entrada 											 ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	RestArea( aAreaRdh )
	RestArea( aAreaRd5 )
	RestArea( aArea )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Restaura o Estado do Ponteiro do Mouse						   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	CursorArrow()

Return( NIL )

/*/{Protheus.doc} InApda040Exec
Executar Funcoes Dentro de APDA040
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 24/08/2004
@param cExecIn, characters, cExecIn
@param aFormParam, array, aFormParam
@return undefined, uRet
/*/
Function InApda040Exec( cExecIn , aFormParam)

	Local uRet

	DEFAULT cExecIn		:= ""
	DEFAULT aFormParam	:= {}

	IF !Empty( cExecIn )
		cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
		uRet	:= &( cExecIn )
	EndIF

Return( uRet )

/*/{Protheus.doc} Apda040Vis
Cadastro de Tipos de Avalica‡„o (Visualizar)
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@param cAlias, characters, cAlias
@param nReg, numeric, nReg
/*/
Function Apda040Vis( cAlias , nReg )
Return( APDA040( cAlias , nReg , 2 ) )

/*/{Protheus.doc} Apda040Inc
Cadastro de Tipos de Avalica‡„o (Incluir)
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@param cAlias, characters, cAlias
@param nReg, numeric, nReg
/*/
Function Apda040Inc( cAlias , nReg )
Return( APDA040( cAlias , nReg , 3 ) )

/*/{Protheus.doc} Apda040Alt
Cadastro de Tipos de Avalica‡„o (Alterar)
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@param cAlias, characters, cAlias
@param nReg, numeric, nReg
/*/
Function Apda040Alt( cAlias , nReg )
Return( APDA040( cAlias , nReg , 4 ) )

/*/{Protheus.doc} Apda040Del
Cadastro de Tipos de Avalica‡„o (Excluir)
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@param cAlias, characters, cAlias
@param nReg, numeric, nReg
/*/
Function Apda040Del( cAlias , nReg )
Return( APDA040( cAlias , nReg , 5 ) )

/*/{Protheus.doc} Apda040Mnt
Cadastro de Tipos de Manuten‡„o
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@param cAlias, characters, cAlias
@param nReg, numeric, nReg
@param nOpc, numeric, nOpc
@param lDlgPadSiga, logical, lDlgPadSiga
@return numeric, Apda040Mnt
/*/
Function Apda040Mnt( cAlias , nReg , nOpc , lDlgPadSiga )

	Local aArea			:= GetArea(Alias())
	Local aSvKeys		:= GetKeys()
	Local aAdvSize		:= {}
	Local aInfoAdvSize	:= {}
	Local aObjSize		:= {}
	Local aObjCoords	:= {}
	Local aRd5Header	:= {}
	Local aRd5Cols		:= {}
	Local aSvRd5Cols	:= {}
	Local aRd5Fields	:= {}
	Local aRd5Altera	:= {}
	Local aRd5NaoAlt	:= {}
	Local aRd5VirtEn	:= {}
	Local aRd5NotFields	:= {}
	Local aRd5Recnos	:= {}
	Local aRd5Keys		:= {}
	Local aRd5VisuEn	:= {}
	Local aRdhGdAltera  := {}
	Local aRdhGdNaoAlt	:= {}
	Local aRdhRecnos	:= {}
	Local aRdhKeys		:= {}
	Local aRdhNotFields	:= {}
	Local aRdhVirtGd	:= {}
	Local aRdhVisuGd	:= {}
	Local aRdhHeader	:= {}
	Local aRdhCols		:= {}
	Local aSvRdhCols	:= {}
	Local aRdhQuery		:= {}
	Local aButtons		:= {}
	Local aFreeLocks	:= {}
	Local aLog			:= {}
	Local aLogTitle		:= {}
	Local aLogGer		:= {}
	Local aLogGerTitle	:= {}
	Local bRdhGdDelOk	:= { |lDelOk| CursorWait() , lDelOk := RdhGdDelOk( "RDH" , NIL , nOpc , cRd5CodTip , nRdhOrder ) , CursorArrow() , lDelOk }
	Local bSet15		:= { || NIL }
	Local bSet24		:= { || NIL }
	Local bGdRdhSeek	:= { || NIL }
	Local bDialogInit	:= { || NIL }
	Local bGetRd5		:= { || NIL }
	Local bGetRdh		:= { || NIL }
	Local bRdhSort		:= { || NIL }
	Local cFilRD5		:= ""
	Local cRd5CodTip	:= ""
	Local cRD5KeySeek	:= ""
	Local cMsgYesNo		:= ""
	Local cTitLog		:= ""
	Local lLocks		:= .F.
	Local lExecLock		:= ( ( nOpc <> 2 ) .and. ( nOpc <> 3 ) )
	Local lExcGeraLog	:= .F.
	Local nOpcAlt		:= 0
	Local nRD5Usado		:= 0
	Local nRDHUsado		:= 0
	Local nLoop			:= 0
	Local nLoops		:= 0
	Local nOpcNewGd		:= IF( ( ( nOpc == 2 ) .or. ( nOpc == 5 ) ) , 0 , GD_INSERT + GD_UPDATE + GD_DELETE	)
	Local nRdhMaxLocks	:= 50
	Local nRdhGhostCol	:= 0
	Local nRdhOrder		:= RetOrdem( "RDH" , "RDH_FILIAL+RDH_CODTIP+RDH_CODNET" )
	Local oDlg			:= NIL
	Local oEnRd5		:= NIL
	Local oGdRdh		:= NIL

	Private aGets
	Private aTela

	Private nGetSX8Len	:= GetSX8Len()

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Poe o Ponteiro do Mouse em Estado de Espera				   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	CursorWait()

	Begin Sequence

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Checa a Opcao Selecionada									   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		aRotSetOpc( cAlias , @nReg , nOpc )

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Monta os Dados para a Enchoice							   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		aRd5NotFields	:= { "RD5_FILIAL" }
		bGetRd5			:= { |lLock,lExclu|	IF( lExecLock , ( lLock := .T. , lExclu	:= .T. ) , aRd5Keys := NIL ),;
		aRd5Cols := RD5->(;
		GdBuildCols(	@aRd5Header		,;	//01 -> Array com os Campos do Cabecalho da GetDados
		@nRd5Usado		,;	//02 -> Numero de Campos em Uso
		@aRd5VirtEn		,;	//03 -> [@]Array com os Campos Virtuais
		@aRd5VisuEn		,;	//04 -> [@]Array com os Campos Visuais
		"RD5"			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
		aRd5NotFields	,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
		@aRd5Recnos		,;	//07 -> [@]Array unidimensional contendo os Recnos
		"RD5"		   	,;	//08 -> Alias do Arquivo Pai
		NIL				,;	//09 -> Chave para o Posicionamento no Alias Filho
		NIL				,;	//10 -> Bloco para condicao de Loop While
		NIL				,;	//11 -> Bloco para Skip no Loop While
		NIL				,;	//12 -> Se Havera o Elemento de Delecao no aCols
		NIL				,;	//13 -> Se Sera considerado o Inicializador Padrao
		NIL				,;	//14 -> Opcional, Carregar Todos os Campos
		NIL				,;	//15 -> Opcional, Nao Carregar os Campos Virtuais
		NIL				,;	//16 -> Opcional, Utilizacao de Query para Selecao de Dados
		NIL				,;	//17 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
		NIL				,;	//18 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
		NIL				,;	//19 -> Carregar Coluna Fantasma
		NIL				,;	//20 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
		NIL				,;	//21 -> Verifica se Deve Checar se o campo eh usado
		NIL				,;	//22 -> Verifica se Deve Checar o nivel do usuario
		NIL				,;	//23 -> Verifica se Deve Carregar o Elemento Vazio no aCols
		@aRd5Keys		,;	//24 -> [@]Array que contera as chaves conforme recnos
		@lLock			,;	//25 -> [@]Se devera efetuar o Lock dos Registros
		@lExclu			 ;	//26 -> [@]Se devera obter a Exclusividade nas chaves dos registros
		);
		),;
		IF( lExecLock , ( lLock .and. lExclu ) , .T. );
		}
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Lock do Registro do RD5									   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF !( lLocks := WhileNoLock( "RD5" , NIL , NIL , 1 , 1 , .T. , 1 , 5 , bGetRd5 ) )
			Break
		EndIF
		aSvRd5Cols		:= aClone( aRd5Cols )
		cFilRD5			:= RD5->RD5_FILIAL
		cRd5CodTip		:= RD5->RD5_CODTIP
		cRD5KeySeek		:= ( cFilRD5 + cRd5CodTip )

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Cria as Variaveis de Memoria e Carrega os Dados Conforme o ar³
		³ quivo														   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		For nLoop := 1 To nRD5Usado
			aAdd( aRd5Fields , aRd5Header[ nLoop , 02 ] )
			SetMemVar( aRd5Header[ nLoop , 02 ] , aRd5Cols[ 01 , nLoop ] , .T. )
		Next nLoop

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Define os Campos Editaveis na Enchoice Apenas na Inclusao( 3 )³
		³ou Alteracao(4)											   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF ( ( nOpc == 3 ) .or. ( nOpc == 4 ) )

			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Define os Campos Editaveis								   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			nLoops := Len( aRd5VisuEn )
			For nLoop := 1 To nLoops
				aAdd( aRd5NaoAlt , aRd5VisuEn[ nLoop ] )
			Next nLoop
			IF ( nOpc == 4 )
				aAdd( aRd5NaoAlt , "RD5_CODTIP" )
			EndIF
			nLoops := Len( aRd5Fields )
			For nLoop := 1 To nLoops
				IF ( aScan( aRd5NaoAlt , { |cNaoA| cNaoA == aRd5Fields[ nLoop ] } ) == 0 )
					aAdd( aRd5Altera , aRd5Fields[ nLoop ] )
				EndIF
			Next nLoop

		EndIF

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Monta os Dados para a GetDados							   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		aAdd( aRdhNotFields , "RDH_FILIAL"  )
		aAdd( aRdhNotFields , "RDH_CODTIP"	)
		aRdhQuery		:= Array( 05 )
		aRdhQuery[01]	:= "RDH_FILIAL='"+cFilRD5+"'"
		aRdhQuery[02]	:= " AND "
		aRdhQuery[03]	:= "RDH_CODTIP='"+cRd5CodTip+"'"
		aRdhQuery[04]	:= " AND "
		aRdhQuery[05]	:= "D_E_L_E_T_=' ' "
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Quando For Inclusao Posiciona o RDH No Final do Arquivo	   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF ( nOpc == 3  ) //Inclusao
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Garante que na Inclusao o Ponteiro do RDH estara em Eof()    ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			PutFileInEof( "RDH" )
		EndIF
		bGetRdh	:= { |lLock,lExclu|	IF( lExecLock , ( lLock := .T. , lExclu := .T. ) , aRdhKeys := NIL ),;
		aRdhCols := RDH->(;
		GdBuildCols(	@aRdhHeader		,;	//01 -> Array com os Campos do Cabecalho da GetDados
		@nRdhUsado		,;	//02 -> Numero de Campos em Uso
		@aRdhVirtGd		,;	//03 -> [@]Array com os Campos Virtuais
		@aRdhVisuGd		,;	//04 -> [@]Array com os Campos Visuais
		"RDH"			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
		aRdhNotFields	,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
		@aRdhRecnos		,;	//07 -> [@]Array unidimensional contendo os Recnos
		"RD5"		   	,;	//08 -> Alias do Arquivo Pai
		cRD5KeySeek		,;	//09 -> Chave para o Posicionamento no Alias Filho
		NIL				,;	//10 -> Bloco para condicao de Loop While
		NIL				,;	//11 -> Bloco para Skip no Loop While
		NIL				,;	//12 -> Se Havera o Elemento de Delecao no aCols
		NIL				,;	//13 -> Se Sera considerado o Inicializador Padrao
		NIL				,;	//14 -> Opcional, Carregar Todos os Campos
		NIL				,;	//15 -> Opcional, Nao Carregar os Campos Virtuais
		aRdhQuery		,;	//16 -> Opcional, Utilizacao de Query para Selecao de Dados
		.F.				,;	//17 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
		.F.				,;	//18 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
		Altera			,;	//19 -> Carregar Coluna Fantasma
		NIL				,;	//20 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
		NIL				,;	//21 -> Verifica se Deve Checar se o campo eh usado
		NIL				,;	//22 -> Verifica se Deve Checar o nivel do usuario
		NIL				,;	//23 -> Verifica se Deve Carregar o Elemento Vazio no aCols
		@aRdhKeys		,;	//24 -> [@]Array que contera as chaves conforme recnos
		@lLock			,;	//25 -> [@]Se devera efetuar o Lock dos Registros
		@lExclu			,;	//26 -> [@]Se devera obter a Exclusividade nas chaves dos registros
		nRdhMaxLocks	,;	//27 -> Numero maximo de Locks a ser efetuado
		Altera			 ;	//28 -> Utiliza Numeracao na GhostCol
		);
		),;
		IF( lExecLock , ( lLock .and. lExclu ) , .T. );
		}
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Lock do Registro do RDH									   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF !( lLocks := WhileNoLock( "RDH" , NIL , NIL , 1 , 1 , .T. , nRdhMaxLocks , 5 , bGetRdh ) )
			Break
		EndIF
		CursorWait()

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Monta Bloco para Sort antes da Gravacao					   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF ( ( nRdhGhostCol := GdFieldPos( "GHOSTCOL" , aRdhHeader ) ) > 0 )
			bRdhSort := { |x,y| ( x[ nRdhGhostCol ] < y[ nRdhGhostCol ] ) }
		EndIF

		aSvRdhCols	:= aClone( aRdhCols )
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Carrega os Campos Editaveis para a GetDados				   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		For nLoop := 1	To nRDHUsado
			SetMemVar( aRdhHeader[ nLoop , 02 ] , GetValType( aRdhHeader[ nLoop , 08 ] , aRdhHeader[ nLoop , 04 ] ) , .T. )
			IF (;
			( aScan( aRdhVirtGd		, aRdhHeader[ nLoop , 02 ] ) == 0 ) .and.	;
			( aScan( aRdhVisuGd		, aRdhHeader[ nLoop , 02 ] ) == 0 ) .and.	;
			( aScan( aRdhNotFields	, aRdhHeader[ nLoop , 02 ] ) == 0 ) .and.	;
			( aScan( aRdhGdNaoAlt	, aRdhHeader[ nLoop , 02 ] ) == 0 )			;
			)
				aAdd( aRdhGdAltera , aRdhHeader[ nLoop , 02 ] )
			EndIF
		Next nLoop

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Verifica se Pode Efetuar a Delecao dos Registros			   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF ( nOpc == 5 )
			cMsgYesNo	:= OemToAnsi(;
			STR0018 + ;	//"O sistema ir  efetuar a verifica‡„o para ver se o registro selecionado"
			CRLF	+ ;
			STR0019 + ;	//"para exclus„o est  sendo utilizado. A verifica‡„o pode ser demorada."
			CRLF	+ ;
			CRLF	+ ;
			STR0020	  ;	//"Confirma a exclus„o?"
			)
			cTitLog		:= OemToAnsi( STR0021 )	//"Log de verifica‡„o de exclus„o"
			CursorArrow()
			IF ( MsgYesNo( OemToAnsi( cMsgYesNo ) , cCadastro + " - " + OemToAnsi( cTitLog ) ) )
				CursorWait()
				nRdhCodNet := GdFieldPos( "RDH_CODNET" , aRdhHeader )
				RDH->( dbSetOrder( nRdhOrder ) )
				nLoops := Len( aRdhRecnos )
				For nLoop := 1 To nLoops
					IF !( ApdChkDel( "RDH" , aRdhRecnos[ nLoop ] , nOpc , ( cRd5CodTip + aRdhCols[ nLoop , nRdhCodNet ] ) , .F. , @aLog , @aLogTitle ) )
						aAdd( aLogGer , aClone( aLog ) )
						aAdd( aLogGerTitle , aLogTitle[1] )
					EndIF
				Next nLoop
				IF !( lExcGeraLog := !Empty( aLogGer ) )
					IF !( ApdChkDel( cAlias , nReg , nOpc , cRd5CodTip , .F. , @aLog , @aLogTitle , { "RDH" } ) )
						aAdd( aLogGer , aClone( aLog ) )
						aAdd( aLogGerTitle , aLogTitle[1] )
					EndIF
				EndIF
				IF ( lExcGeraLog := !Empty( aLogGer ) )
					CursorArrow()
					//"Deseja gerar Log?"
					IF ( lExcGeraLog := MsgNoYes( STR0022 , cCadastro + " - " + OemToAnsi( cTitLog ) ) )
						CursorWait()
						//"Log de Inconsistencia na Exclusao de Tipos de Avaliacao"
						fMakeLog( aLogGer , aLogGerTitle , NIL , NIL , FunName() , STR0023 )
						CursorArrow()
					Else
						//"A chave a ser excluida est  sendo utilizada."
						//"At‚ que as referˆncias a ela sejam eliminadas a mesma n„o pode ser excluida."
						MsgInfo( OemToAnsi( STR0024 + CRLF + STR0025 ) , cCadastro + " - " + OemToAnsi( cTitLog ) )
					EndIF
					Break
				EndIF
			Else
				Break
			EndIF
			CursorWait()
		EndIF

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Monta as Dimensoes dos Objetos         					   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		DEFAULT lDlgPadSiga	:= .F.
		aAdvSize		:= MsAdvSize( NIL , lDlgPadSiga )
		aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 2 , 2 }
		aAdd( aObjCoords , { 000 , 058 , .T. , .F. } )
		aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
		aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Define o Botao de Pesquisa na GetDados					   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		bGdRdhSeek := { ||	GdRdhSeek( oGdRdh )				,;
		SetKey( VK_F4 , bGdRdhSeek )	 ;
		}
		aAdd(;
		aButtons	,;
		{;
		"pesquisa",;
		bGdRdhSeek,;
		OemToAnsi( STR0001 + "...<F4>"  ),;	//"Pesquisar"
		OemToAnsi( STR0001 );				//"Pesquisar"
		};
		)

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Define o Bloco para a Tecla <CTRL-O> 						   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		bSet15		:= { || IF(;
		(;
		( nOpc == 3 );	//Inclusao
		.or.;
		( nOpc == 4 );	//Alteracao
		);
		.and.;
		Apda040TEncOk( nOpc , oEnRd5 );							//Valida Todos os Campos da Enchoice
		.and.;
		oGdRdh:TudoOk(),;										//Valida as Informacoes da GetDados
		(;
		nOpcAlt 	:= 1 ,;
		aRdhCols	:= oGdRdh:aCols,;						//Redireciona o Ponteiro do aRdhCols
		RestKeys( aSvKeys , .T. ),;
		oDlg:End();
		),;
		IF(;
		(;
		( nOpc == 3 );	//Inclusao
		.or.;
		( nOpc == 4 );  //Alteracao
		) ,;
		(;
		nOpcAlt := 0 ,;
		.F.;
		),;
		(;
		nOpcAlt := IF( nOpc == 2 , 0 , 1 ) ,;		//Visualizacao ou Exclusao
		RestKeys( aSvKeys , .T. ),;
		oDlg:End();
		);
		);
		);
		}
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Define o Bloco para a Teclas <CTRL-X>     	   			   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		bSet24		:= { || ( nOpcAlt := 0 , RestKeys( aSvKeys , .T. ) , oDlg:End() ) }

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Define o Bloco para o Init do Dialog						   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		bDialogInit := { ||;
		EnchoiceBar( oDlg , bSet15 , bSet24 , NIL , aButtons ),;
		SetKey( VK_F4 , bGdRdhSeek  ),;
		}

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Monta o Dialogo Principal para a Manutencao das Formulas	   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		DEFINE MSDIALOG oDlg TITLE OemToAnsi( STR0006 ) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF GetWndDefault() PIXEL
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Monta o Objeto Enchoice para o RD5                      	   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		oEnRd5	:= MsmGet():New(	cAlias		,;
		nReg		,;
		nOpc		,;
		NIL			,;
		NIL			,;
		NIL			,;
		aRd5Fields	,;
		aObjSize[1]	,;
		aRd5Altera	,;
		NIL			,;
		NIL			,;
		NIL			,;
		oDlg		,;
		NIL			,;
		.F.			,;
		NIL			,;
		.F.			 ;
		)
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Monta o Objeto GetDados para o RDH						   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		oGdRdh	:= MsNewGetDados():New(	aObjSize[2,1]													,;
		aObjSize[2,2]													,;
		aObjSize[2,3]													,;
		aObjSize[2,4]													,;
		nOpcNewGd														,;
		"oGdRdhLinOk"													,;
		"oGdRdhTudOk"													,;
		""																,;
		aRdhGdAltera													,;
		0																,;
		Val(Replicate("9",GetSx3Cache( "RDH_CODNET" , "X3_TAMANHO" )))	,;
		NIL																,;
		NIL																,;
		bRdhGdDelOk														,;
		oDlg															,;
		aRdhHeader														,;
		aRdhCols									 					 ;
		)
		ACTIVATE MSDIALOG oDlg ON INIT Eval( bDialogInit ) CENTERED

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Coloca o Ponteiro do Mouse em Estado de Espera			   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		CursorWait()

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Quando Confirmada a Opcao e Nao for Visualizacao Grava ou   Ex³
		³clui as Informacoes do RD5 e RDH							   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF( nOpcAlt == 1 )
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Apenas se nao For Visualizacao              				   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			IF ( nOpc != 2 )
				/*/
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Gravando/Incluido ou Excluindo Informacoes do SRY/RDH        ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
				MsAguarde(;
				{ ||;
				aSort( aRdhCols , NIL , NIL , bRdhSort ),;	//Sorteia as Informacoes do RDh para Comparacao Antes da Gravacao
				Apda040Grava(;
				nOpc		,;	//Opcao de Acordo com aRotina
				nReg		,;	//Numero do Registro do Arquivo Pai ( RD5 )
				aRd5Header	,;	//Campos do Arquivo Pai ( RD5 )
				aRd5VirtEn	,;	//Campos Virtuais do Arquivo Pai ( RD5 )
				aRd5Cols	,;	//Conteudo Atual dos Campos do Arquivo Pai ( RD5 )
				aSvRd5Cols	,;	//Conteudo Anterior dos Campos do Arquivo Pai ( RD5 )
				aRdhHeader	,;	//Campos do Arquivo Filho ( RDH )
				aRdhCols	,;	//Itens Atual do Arquivo Filho ( RDH )
				aSvRdhCols	,;	//Itens Anterior do Arquivo Filho ( RDH )
				aRdhVirtGd	,;	//Campos Virtuais do Arquivo Filho ( RDH )
				aRdhRecnos	 ;	//Recnos do Arquivo Filho ( RDH )
				);
				};
				)
			EndIF
		Else
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ RollBack da Numeracao Automatica            				   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			While ( GetSX8Len() > nGetSX8Len )
				RollBackSX8()
			End While
		EndIF

	End Sequence

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Libera os Locks             								   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	aAdd( aFreeLocks , { "RD5" , aRd5Recnos , aRd5Keys } )
	aAdd( aFreeLocks , { "RDH" , aRdhRecnos , aRdhKeys } )
	ApdFreeLocks( aFreeLocks )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Restaura os Dados de Entrada								   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	RestArea( aArea )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Restaura as Teclas de Atalho                                  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	RestKeys( aSvKeys , .T. )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Restaura o Ponteiro do Mouse1								   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	CursorArrow()

Return( nOpcAlt )

/*/{Protheus.doc} GdRdhSeek
Efetuar Pesquisa na GetDados
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@param oGdRdh, object, oGdRdh
/*/
Static Function GdRdhSeek( oGdRdh )

	Local aSvKeys		:= GetKeys()
	Local cProcName3	:= Upper( AllTrim( ProcName( 3 ) ) )
	Local cProcName5	:= Upper( AllTrim( ProcName( 5 ) ) )

	Begin Sequence

		IF !( "APDA040MNT" $ ( cProcName3 + cProcName5  ) )
			Break
		EndIF

		GdSeek( oGdRdh , OemToAnsi( STR0001 ) )	//"Pesquisar"

	End Sequence

	RestKeys( aSvKeys , .T. )

Return( NIL )

/*/{Protheus.doc} Apda040TEncOk
TudoOk para a Enchoice
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@param nOpc, numeric, nOpc
@param oEnRd5, object, nOpc
@return logical, lTudoOk
/*/
Static Function Apda040TEncOk( nOpc , oEnRd5 )

	Local lTudoOk := .T.

	IF ( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
		lTudoOk := EnchoTudOk( oEnRd5 )
	EndIF
	RstEnchoVlds()
Return( lTudoOk )

/*/{Protheus.doc} oGdRdhLinOk
Definir
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@param oBrowse, object, descricao
@return Logical, lLinOk
/*/
Function oGdRdhLinOk( oBrowse )

	Local lLinOk  		:= .T.

	Local aCposKey
	Local cRdhAutoAv
	Local cRdhNivel

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Altera o Estado do Cursor  								   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	CursorWait()

	Begin Sequence
		IF !( GdDeleted() )
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Verifica Se o Campos Estao Devidamente Preenchidos		   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			aCposKey := GdObrigat( aHeader )
			IF !( lLinOk := GdNoEmpty( aCposKey ) )
				Break
			EndIF
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Verifica Itens Duplicados na GetDados						   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			aCposKey := GetArrUniqe( "RDH" )
			IF !( lLinOk := GdCheckKey( aCposKey , 4 ) )
				Break
			EndIF
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Carrega o Conteudo dos Campos RDH_AUTOAV e RDH_NIVEL         ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			cRdhAutoAv	:= GdFieldGet( "RDH_AUTOAV" )
			cRdhNivel	:= GdFieldGet( "RDH_NIVEL"  )
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Auto-Avaliacao so no Mesmo Nivel                             ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			IF !( lLinOk := !( ( cRdhAutoAv == "1" ) .and. ( cRdhNivel != "1" ) ) )
				MsgInfo(;
				OemToAnsi( STR0007 )	,;	//"A Auto-Avalia‡„o s¢ pode ser definida quando for o 'Mesmo N¡vel'."
				OemToAnsi( STR0008 ) 	 ;	//"Aviso de Inconsistˆncia!"
				)
				Break
			EndIF
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Verifica Itens Duplicados na GetDados. A Auto-Avaliacao so po³
			³ de ser Definida para um Unico Item						   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			IF ( cRdhAutoAv  == "1" )
				aCposKey := { "RDH_AUTOAV" }
				IF !( lLinOk := GdCheckKey( aCposKey , 4 ) )
					Break
				EndIF
			EndIF
		EndIF

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Reinicializa RDH_CODNET                                      ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		SetMemVar( "RDH_CODNET" , Space( GetSx3Cache( "RDH_CODNET" , "X3_TAMANHO" ) ) )

	End Sequence

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Se Houver Alguma Inconsistencia na GetDados, Seta-lhe o Foco  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF !( lLinOk )
		oBrowse:SetFocus()
	EndIF

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Garante que na Inclusao o Ponteiro do RDH estara em Eof()    ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	PutFileInEof( "RDH" )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Restaura o Estado do Cursor								   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	CursorArrow()

Return( lLinOk )

/*/{Protheus.doc} oGdRdhTudOk
Definir
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@param oBrowse, object, descricao
@return Logical, lLinOk
/*/
Function oGdRdhTudOk( oBrowse )

	Local lTudoOk 	:= .T.
	Local nAvaCon	:= 0
	Local nAvaDor	:= 0

	Local nLoop

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Altera o Estado do Cursor  								   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	CursorWait()

	Begin Sequence

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Percorre Todas as Linhas para verificar se Esta Tudo OK      ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		For nLoop := 1 To Len( aCols )
			n := nLoop
			If GdFieldGet( "RDH_AUTOAV" ) == "2" .and. GdFieldGet( "RDH_FEEDBK" ) == "2"
				nAvaDor++
			EndIf
			If GdFieldGet( "RDH_FEEDBK" ) == "1"
				nAvaCon++
			EndIf
			If !( lTudoOk := oGdRdhLinOk( oBrowse ) )
				oBrowse:Refresh()
				Break
			EndIf
		Next nLoop

		If nAvaDor > 1 .and. nAvaCon == 0
			Help( ,, "Atenção",, "Quando existe mais de um avaliador na definição de redes, é obrigatório a existência de consenso.", 1, 0 )
			lTudoOk := .F.
		ElseIf nAvaCon > 1
			Help( ,, "Atenção",, "O consenso deve ser habilitado em apenas uma rede por tipo de avaliação.", 1, 0 )
			lTudoOk := .F.
		EndIf
	End Sequence

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Restaura o Estado do Cursor								   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	CursorArrow()

Return( lTudoOk  )

/*/{Protheus.doc} RdhGdDelOk
Validar a Delecao na GetDados
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@param cAlias, characters, cAlias
@param nRecno, numeric, nRecno
@param nOpc, numeric, nOpc
@param cCodigo, characters, cCodigo
@param nRdhOrder, numeric, nRdhOrder
@return Logical, lDelOk
/*/
Static Function RdhGdDelOk( cAlias , nRecno , nOpc , cCodigo , nRdhOrder )

	Local lDelOk 		:= .T.
	Local lStatusDel	:= .F.

	Static lFirstDelOk
	Static lLstDelOk

	DEFAULT lFirstDelOk	:= .T.
	DEFAULT lLstDelOk	:= .T.

	Begin Sequence

		//Quando for Visualizacao ou Exclusao Abandona
		IF (;
		( nOpc == 2 ) .or. ;	//Visualizacao
		( nOpc == 5 );			//Exclusao
		)
			Break
		EndIF

		//Apenas se for a primeira vez
		IF !( lFirstDelOk )
			lFirstDelOk	:= .T.
			lDelOk 		:= lLstDelOk
			lLstDelOk	:= .T.
			Break
		EndIF

		lStatusDel	:= !( GdDeleted() ) //Inverte o Estado

		IF ( lStatusDel )	//Deletar
			IF !( nOpc == 3  )	//Quando nao for Inclusao
				RDH->( dbSetOrder( nRdhOrder ) )
				IF !( lDelOk := ApdChkDel( cAlias , nRecno , nOpc , ( cCodigo + GdFieldGet( "RDH_CODNET" ) ) , .F. , NIL , NIL , NIL , NIL , .T. ) )
					CursorArrow()
					//"A chave a ser excluida est  sendo utilizada."
					//"At‚ que as referˆncias a ela sejam eliminadas a mesma n„o pode ser excluida."
					MsgInfo( OemToAnsi( STR0016 + CRLF + STR0017 ) , cCadastro )
					lLstDelOk := lDelOk
					//Ja Passou pela funcao
					lFirstDelOk := .F.
					Break
				EndIF
			EndIF
		Else				//Restaurar
			lLstDelOk := lDelOk
			//Ja Passou pela funcao
			lFirstDelOk := .F.
			Break
		EndIF
		//Ja Passou pela funcao
		lFirstDelOk := .F.
		SetMemVar( "RDH_CODNET" , Space( GetSx3Cache( "RDH_CODNET" , "X3_TAMANHO" ) ) )

	End Sequence

Return( lDelOk )

/*/{Protheus.doc} Apda040Grava
Definir
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@param nOpc, numeric, Opcao de Acordo com aRotina
@param nReg, numeric, Numero do Registro do Arquivo Pai ( RD5 )
@param aRd5Header, array, Campos do Arquivo Pai ( RD5 )
@param aRd5VirtEn, array, Campos Virtuais do Arquivo Pai ( RD5 )
@param aRd5Cols, array, Conteudo Atual dos Campos do Arquivo Pai ( RD5 )
@param aSvRd5Cols, array, Conteudo Anterior dos Campos do Arquivo Pai ( RD5 )
@param aRdhHeader, array, Campos do Arquivo Filho ( RDH )
@param aRdhCols, array, Itens Atual do Arquivo Filho ( RDH )
@param aSvRdhCols, array, Itens Anterior do Arquivo Filho ( RDH )
@param aRdhVirtGd, array, Campos Virtuais do Arquivo Filho ( RDH )
@param aRdhRecnos, array, Recnos do Arquivo Filho ( RDH )
/*/
Static Function Apda040Grava(nOpc, nReg, aRd5Header, aRd5VirtEn, aRd5Cols, aSvRd5Cols, aRdhHeader, aRdhCols, aSvRdhCols, aRdhVirtGd, aRdhRecnos)

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Variaveis de Inicializacao Obrigatoria					  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	Local aMestre	:= GdPutIStrMestre( 01 )
	Local aItens	:= {}
	Local cOpcao	:= IF( ( nOpc == 5 ) , "DELETE" , IF( ( ( nOpc == 3 ) .or. ( nOpc == 4 ) ) , "PUT" , NIL ) )
	Local lAllModif	:= .F.
	Local lRd5Modif	:= .F.
	Local lRdhModif	:= .F.
	Local lRdhDelet	:= .F.

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Variaveis que serao inicializadas no Corpo da Funcao		  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	Local aRdhColDel
	Local aRdhRecDel
	Local nLoop
	Local nLoops
	Local nItens

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Altera o Estado do Cursor  								   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	CursorWait()

	IF ( cOpcao <> "DELETE" )
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Verifica se Houveram Modificacoes no RDV					   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF ( lRdhModif := !ArrayCompare( aRdhCols , aSvRdhCols ) )
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Deleto todos os Itens que nao Estao OK					   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			GdSuperDel( aRdhHeader , @aRdhCols , NIL , .T. , GdGetBlock( "RDH" , aRdhHeader , .F. ) )
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Separa os Itens que foram Deletados     					   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			lRdhDelet := GdSplitDel( aRdhHeader , @aRdhCols , @aRdhRecnos , @aRdhColDel , @aRdhRecDel  )
		EndIF
	Else
		lRdhModif := .T.
		lRd5Modif := .T.
	EndIF

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Carrega os Itens Apenas se Houveram Alteracoes ou na Exclusao³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF ( lRdhModif )

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Carrega as Informacoes deletadas RDH                   	   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF ( lRdhDelet )

			aAdd( aItens , GdPutIStrItens() )
			nItens := Len( aItens )

			aItens[ nItens , 01 ] := "RDH"
			aItens[ nItens , 02 ] := NIL
			aItens[ nItens , 03 ] := aClone( aRdhHeader )
			aItens[ nItens , 04 ] := aClone( aRdhColDel )
			aItens[ nItens , 05 ] := aClone( aRdhVirtGd )
			aItens[ nItens , 06 ] := aClone( aRdhRecDel )

		EndIF

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Carrega as Informacoes do RDH                   			   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		aAdd( aItens , GdPutIStrItens() )
		nItens := Len( aItens )
		aItens[ nItens , 01 ] := "RDH"
		aItens[ nItens , 02 ] := {;
		{ "FILIAL" , xFilial( "RDH" , xFilial( "RD5" ) ) },;
		{ "CODTIP" , GetMemVar( "RD5_CODTIP" ) };
		}
		aItens[ nItens , 03 ] := aClone( aRdhHeader )
		aItens[ nItens , 04 ] := aClone( aRdhCols   )
		aItens[ nItens , 05 ] := aClone( aRdhVirtGd )
		aItens[ nItens , 06 ] := aClone( aRdhRecnos )

	EndIF

	IF !( lRd5Modif )
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Atualiza aRdmCols para Verificar se Houveram Alteracoes	   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		nLoops := Len( aRd5Header )
		For nLoop := 1 To nLoops
			aRd5Cols[ 01 , nLoop ] := GetMemVar( aRd5Header[ nLoop , 02 ] )
		Next nLoop
		lRd5Modif := !( ArrayCompare( aRd5Cols , aSvRd5Cols ) )
	EndIF

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Seta a Gravacao ou Exclusao Apenas se Houveram Alteracoes  ou³
	³ se foi Selecionada a Exclusao								   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	lAllModif := ( ( lRdhModif ) .or. ( lRd5Modif ) )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Se Houveram Alteracoes, ou Exclusao efetua a Gravacao        ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF ( lAllModif )

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Seta a Gravacao ou Exclusao Apenas se Houveram Alteracoes  ou³
		³ se foi Selecionada a Exclusao								   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		aMestre[ 01 , 01 ]	:= "RD5"
		aMestre[ 01 , 02 ]	:= nReg
		aMestre[ 01 , 03 ]	:= lRd5Modif
		aMestre[ 01 , 04 ]	:= aClone( aRd5Header )
		aMestre[ 01 , 05 ]	:= aClone( aRd5VirtEn )
		aMestre[ 01 , 06 ]	:= {}
		aMestre[ 01 , 07 ]	:= aClone( aItens )

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Grava as Informacoes                        				   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		GdPutInfoData( aMestre , cOpcao , .F. , .F. )

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Confirmando a Numeracao Automatica          				   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		While ( GetSX8Len() > nGetSX8Len )
			ConfirmSX8()
		End While

	EndIF

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Restaura o Estado do Cursor								   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	CursorArrow()

Return( NIL )

/*/{Protheus.doc} Rd5CodTipVld
Funcao para Validar o Conteudo do Campo RD5_CODTIP
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, lRd5CodTipOk
/*/
Function Rd5CodTipVld()

	Local cRd5CodTip	:= GetMemVar( "RD5_CODTIP" )
	Local lRd5CodTipOk	:= .T.

	Begin Sequence

		IF !( lRd5CodTipOk := Rd5GetCodTip( @cRd5CodTip , .F. , .F. ) )
			Break
		EndIF

		SetMemVar( "RD5_CODTIP" , cRd5CodTip )

	End Sequence

Return( lRd5CodTipOk )

/*/{Protheus.doc} Rd5GetCodTip
Obtem Numeracao Valida para o RD5_CODTIP
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@param cRd5CodTip, characters, cRd5CodTip
@param lExistChav, logical, lExistChav
@param lShowHelp, logical, lShowHelp
@return Numeric, Numeracao Valida para o RD5_CODTIP
/*/
Function Rd5GetCodTip( cRd5CodTip , lExistChav , lShowHelp )
Return(;
GetNrExclOk(	@cRd5CodTip 			,;
"RD5"					,;
"RD5_CODTIP"			,;
"RD5_FILIAL+RD5_CODTIP" ,;
NIL						,;
lExistChav				,;
lShowHelp	 			 ;
);
)


/*/{Protheus.doc} Rd5CodTipInit
Inicializador Padrao do Campo RD5_CODTIP
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return characters, cRd5CodTip
/*/
Function Rd5CodTipInit()
	Local cRd5CodTip
	Rd5GetCodTip( @cRd5CodTip , .F. , .F. )
Return( cRd5CodTip )

/*/{Protheus.doc} Rd5DescVld
Rotina que retorna se o usuÃ¡rio pode ter acesso a todos os campos da tela de transferÃªncia,
conforme o cadastro especifico de usuÃ¡rio para DIMBP (tabela SZK).
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, lRd5DescOk
/*/
Function Rd5DescVld()

	Local lRd5DescOk := .T.

	Begin Sequence

		IF !( lRd5DescOk := NaoVazio() )
			Break
		EndIF

		IF !( lRd5DescOk := Texto() )
			Break
		EndIF

	End Sequence

Return( lRd5DescOk )

/*/{Protheus.doc} Rd5IdentiVld
Funcao para Validar o Conteudo do Campo RD5_IDENTI
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, lRd5IdentiOk
/*/
Function Rd5IdentiVld()

	Local lRd5IdentiOk := .T.

	Begin Sequence

		IF !( lRd5IdentiOk := Pertence( OpBxSimNao( .T. ) ) )
			Break
		EndIF

	End Sequence

Return( lRd5IdentiOk )

/*/{Protheus.doc} Rd5IdentiBox
Funcao para Retornar as Opcoes do Campo RD5_IDENTI
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, Definir
/*/
Function Rd5IdentiBox()
Return( OpBxSimNao( .F. ) )

/*/{Protheus.doc} Rd5IdentiInit
Funcao para Retornar as Opcoes do Campo RD5_IDENTI
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return characters, Definir
/*/
Function Rd5IdentiInit()
Return( SubStr( OpBxSimNao( .T. ) , 2 , 1 ) )

/*/{Protheus.doc} Rd5TipoVld
Funcao para Validar o Conteudo do Campo RD5_TIPO
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, lRd5Tipo
/*/
Function Rd5TipoVld()

	Local lRd5Tipo := .T.

	Begin Sequence

		IF !( lRd5Tipo := Pertence( "123" ) )
			Break
		EndIF

	End Sequence

Return( lRd5Tipo )

/*/{Protheus.doc} Rd5TipoBox
Funcao para Retornar as Opcoes do Campo RD5_TIPO
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return characters, cRd5TipoBox
/*/
Function Rd5TipoBox()

	Local cRd5TipoBox := ""

	cRd5TipoBox := "1="+STR0014+";"	//"Avaliacao"
	cRd5TipoBox += "2="+STR0015+";"	//"Pesquisa"
	cRd5TipoBox += "3="+STR0026		//"Projeto"

Return( cRd5TipoBox )

/*/{Protheus.doc} Rd5TipoInit
Funcao para Retornar as Opcoes do Campo RD5_TIPO
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return characters, cRd5Tipo
/*/
Function Rd5TipoInit()
	Local cRd5Tipo := ""

	IF RD5->RD5_TIPO == "2"
		cRd5Tipo := STR0015	//"Pesquisa"

	ElseIf RD5->RD5_TIPO == "3"
		cRd5Tipo := STR0026	//"Projeto"

	Else
		cRd5Tipo := STR0014	//"Avaliacao"
	EndIf

Return( cRd5Tipo )

/*/{Protheus.doc} RdhCodNetVld
Valid do Campo RDH_CODNET
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, lRdhCodNetOK
/*/
Function RdhCodNetVld()

	Local cRdhCodNet	:= &( ReadVar() )
	Local lRdhCodNetOK	:= .T.

	Begin Sequence

		IF !( lRdhCodNetOK := NaoVazio() )
			Break
		EndIF

		IF !( lRdhCodNetOK := ExistCpo( "RD1" ) )
			Break
		EndIF

		RdhDescInit( cRdhCodNet , .T. )

	End Sequence

Return( lRdhCodNetOK )

/*/{Protheus.doc} RdhDescInit
Inicializadora Padrao do Campo RDH_DESC
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@param cRdhCodNet, characters, cRdhCodNet
@param lGdFieldPut, logical, lGdFieldPut
@return characters, cRdhDescInit
/*/
Function RdhDescInit( cRdhCodNet , lGdFieldPut )

	Local cRdhDescInit	:= ""
	Local lGetDados		:= .F.

	DEFAULT lGdFieldPut := .F.

	lGetDados := IsInGetDados( { "RDH_DESC" , "RDH_CODNET" } )

	IF ( lGetDados )
		IF ( IsMemVar( "RDH_CODNET" ) )
			DEFAULT cRdhCodNet := GetMemVar( "RDH_CODNET" )
		Else
			DEFAULT cRdhCodNet := GdFieldGet( "RDH_CODNET" )
		EndIF
		cRdhDescInit	:= fDesc( "RD1" , cRdhCodNet , "RD1_DESC" , NIL , NIL , NIL , .F. )
		cRdhDescInit	:= Padr( cRdhDescInit , GetSx3Cache( "RDH_DESC" , "X3_TAMANHO" ) )
		IF ( lGdFieldPut )
			GdFieldPut( "RDH_DESC" , cRdhDescInit )
		EndIF
		SetMemVar( "RDH_DESC" , cRdhDescInit )
	Else
		If RD5->RD5_CODTIP == RDH->RDH_CODTIP
			DEFAULT cRdhCodNet	:= RDH->RDH_CODNET
			cRdhDescInit		:= fDesc( "RD1" , cRdhCodNet , "RD1_DESC" , NIL , NIL , NIL , .F. )
			cRdhDescInit		:= Padr( cRdhDescInit , GetSx3Cache( "RDH_DESC" , "X3_TAMANHO" ) )
		EndIf
	EndIF

Return( cRdhDescInit )

/*/{Protheus.doc} RdhDescBrw
Inicializadora Padrao do Browse para o Campo RDH_DESC
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return characters, Definir
/*/
Function RdhDescBrw()
Return( RdhDescInit() )

/*/{Protheus.doc} RdhCodTipVld
Funcao para Validar o Conteudo do Campo RDH_CODTIP
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, lRdhCodTipOk
/*/
Function RdhCodTipVld()

	Local lRdhCodTipOk := .T.

	Begin Sequence

		IF !( lRdhCodTipOk := NaoVazio() )
			Break
		EndIF

		IF !( lRdhCodTipOk := ExistCpo( "RD5" ) )
			Break
		EndIF

	End Sequence

Return( lRdhCodTipOk )

/*/{Protheus.doc} RdhDescVld
Funcao para Validar o Conteudo do Campo RDH_DESC
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, lRdhDescOk
/*/
Function RdhDescVld()

	Local lRdhDescOk := .T.

	Begin Sequence

		IF !( lRdhDescOk := NaoVazio() )
			Break
		EndIF

		IF !( lRdhDescOk := Texto() )
			Break
		EndIF

	End Sequence

Return( lRdhDescOk )

/*/{Protheus.doc} RdhNivelVld
Funcao para Validar o Conteudo do Campo RDH_NIVEL
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, lRdhNivelOk
/*/
Function RdhNivelVld()

	Local lRdhNivelOk := .T.

	Begin Sequence

		IF !( lRdhNivelOk := Pertence( OpBxNivel( .T. ) ) )
			Break
		EndIF

	End Sequence

Return( lRdhNivelOk )

/*/{Protheus.doc} RdhNivelBox
Funcao para Retornar as Opcoes do Campo RDH_NIVEL
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return characters, Definir
/*/
Function RdhNivelBox()
Return( OpBxNivel( .F. ) )

/*/{Protheus.doc} RdhNivelInit
Funcao para Retornar as Opcoes do Campo RDH_NIVEL
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return characters, Definir
/*/
Function RdhNivelInit()
Return( SubStr( OpBxNivel( .T. ) , 1 , 2 ) )

/*/{Protheus.doc} RdhNumParVld
Funcao para Validar o Conteudo do Campo RDH_NUMPAR
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, lRdhNumParOk
/*/
Function RdhNumParVld()

	Local lRdhNumParOk := .T.

	Begin Sequence

		IF !( lRdhNumParOk := ( Positivo() ) )
			Break
		EndIF

	End Sequence

Return( lRdhNumParOk )

/*/{Protheus.doc} RdhNumNivVld
Funcao para Validar o Conteudo do Campo RDH_NUMNIV
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, lRdhNumNivOk
/*/
Function RdhNumNivVld()

	Local lRdhNumNivOk := .T.

	Begin Sequence

		IF !( lRdhNumNivOk := ( Positivo() ) )
			Break
		EndIF

	End Sequence

Return( lRdhNumNivOk )

/*/{Protheus.doc} RdhAutoAvVld
Funcao para Validar o Conteudo do Campo RDH_AUTOAV
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, lRdhAutoAvOk
/*/
Function RdhAutoAvVld()

	Local lRdhAutoAvOk := .T.

	Begin Sequence

		IF !( lRdhAutoAvOk := Pertence( OpBxSimNao( .T. ) ) )
			Break
		EndIF

	End Sequence

Return( lRdhAutoAvOk )

/*/{Protheus.doc} RdhAutoAvBox
Funcao para Retornar as Opcoes do Campo RDH_AUTOAV
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, Definir
/*/
Function RdhAutoAvBox()
Return( OpBxSimNao( .F. ) )

/*/{Protheus.doc} RdhAutoAvInit
Funcao para Retornar as Opcoes do Campo RDH_AUTOAV
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, Definir
/*/
Function RdhAutoAvInit()
Return( SubStr( OpBxSimNao( .T. ) , 2 , 1 ) )

/*/{Protheus.doc} RdhFeedBkVld
Funcao para Validar o Conteudo do Campo RDH_FEEDBK
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, lRdhFeedBkOk
/*/
Function RdhFeedBkVld()

	Local lRdhFeedBkOk := .T.

	Begin Sequence

		IF !( lRdhFeedBkOk := Pertence( OpBxSimNao( .T. ) ) )
			Break
		EndIF

	End Sequence

Return( lRdhFeedBkOk )

/*/{Protheus.doc} RdhFeedBkBox
Funcao para Retornar as Opcoes do Campo RDH_FEEDBK
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Logical, Definir
/*/
Function RdhFeedBkBox()
Return( OpBxSimNao( .F. ) )

/*/{Protheus.doc} RdhFeedBkBox
Funcao para Retornar as Opcoes do Campo RDH_FEEDBK
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return characters, Definir
/*/
Function RdhFeedBkInit()
Return( SubStr( OpBxSimNao( .T. ) , 2 , 1 ) )

/*/{Protheus.doc} MenuDef
Isola opcoes de menu para que as opcoes da rotina possam
ser lidas pelas bibliotecas Framework da Versao 9.12 .
@project MAN0000038865_EF_002
@type function Rotina EspecÃ­fica
@version P12
@author TOTVS
@since 22/10/2018
@return Array, aRotina
/*/
Static Function MenuDef()

	Local aRotina := {;
	{ STR0001 , "AxPesqui"	 , 0 , 01,,.F.} ,; //"Pesquisar"
	{ STR0002 , "Apda040Mnt" , 0 , 02 } ,; //"Visualizar"
	{ STR0003 , "Apda040Mnt" , 0 , 03 } ,; //"Incluir"
	{ STR0004 , "Apda040Mnt" , 0 , 04 } ,; //"Alterar"
	{ STR0005 , "Apda040Mnt" , 0 , 05 }  ; //"Excluir"
	}
Return aRotina